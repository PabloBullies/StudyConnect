plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
    id 'org.sonarqube' version '5.1.0.4882'
}

group = 'pb.studyconnect'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

sonar {
    properties {
        property "sonar.projectKey", "pb.studyconnect:StudyConnectMaster"
        property "sonar.projectName", "StudyConnectMaster"
    }
}

sourceSets {
    itest {
        java {
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.mapstruct:mapstruct:1.6.2'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
    implementation 'jakarta.validation:jakarta.validation-api:3.1.0'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.3.3'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.2'

    itestImplementation 'io.rest-assured:rest-assured:5.5.0'
    itestImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    itestRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    //testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jar {
    enabled = false
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}
check.dependsOn jacocoTestCoverageVerification

tasks.register('itest', Test) {
    testClassesDirs = sourceSets.itest.output.classesDirs
    classpath = sourceSets.itest.runtimeClasspath
    systemProperties(System.getProperties())
    useJUnitPlatform()
}

tasks.register('itestJar', Jar) {
    dependsOn(compileJava)
    dependsOn(processResources)
    from {
       sourceSets.itest.runtimeClasspath.filter { it.name != 'main' }.collect { it.isDirectory() ? it : zipTree(it) }
    }
    archiveBaseName = 'itestJar'
    from sourceSets.itest.output
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}
